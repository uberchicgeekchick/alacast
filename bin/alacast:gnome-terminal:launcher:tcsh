#!/bin/tcsh -f

set alacasts_path="`dirname '${0}'`";
cd "${alacasts_path}";
set alacasts_path="${cwd}";
set alacasts_exec="${alacasts_path}/alacast.php";
if(! -x ${alacasts_exec} ) then
	printf "Unable to find alacast.php or its not exectuable.\n\tAttempting to run: %s" "${alacasts_exec}";
	exit -1;
endif

if(!(${?ALACASTS_OPTIONS})) setenv ALACASTS_OPTIONS '--logging --titles-append-pubdate --strip-characters=#;! --update=detailed --interactive';


set alacasts_resolution="";
if ( -e "${HOME}/.alacast/alacast.ini" ) then
	set alacasts_ini="${HOME}/.alacast/alacast.ini";
else if( -e "${HOME}/.alacast/profiles/${USER}/alacast.ini" ) then
	set alacasts_ini="${HOME}/.alacast/profiles/${USER}/alacast.ini";
else if ( -e "${alacasts_path}/../data/${USER}/default/alacast.ini" ) then
	set alacasts_ini="${alacasts_path}/../data/profiles/${USER}/alacast.ini";
else if ( -e "${alacasts_path}/../data/profiles/default/alacast.ini" ) then
	set alacasts_ini="${alacasts_path}/../data/profiles/default/alacast.ini";
endif

if( ${?alacasts_ini} ) then
	if( ${?GREP_OPTIONS} ) then
		set grep_options="${GREP_OPTIONS}";
		unsetenv GREP_OPTIONS;
	endif
	set alacasts_resolution="`/bin/grep 'resolutions.terminal' "\""${alacasts_ini}"\"" | sed -r 's/.*resolutions\.terminal\="\""([^"\""]+)"\"";/\1/'`";
	if( ${?grep_options} ) then
		setenv GREP_OPTIONS "${grep_options}";
		unset grep_options;
	endif
else
	set alacasts_resolution="140x40";
endif

if(! ${?noglob} ) \
	set noglob;

/usr/bin/gnome-terminal							\
	--geometry="${alacasts_resolution}"				\
	--hide-menubar							\
	--role='Media:Alacast'						\
	--title="[`alacast:php`] #>"					\
	--working-directory="${alacasts_path}"				\
	--command="${alacasts_exec} --mode=update ${argv}" &;
